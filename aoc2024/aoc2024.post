/// lib array utils
fn len<T>(a: [T]) int external
fn set_size<T>(a: [T], new_size: int) external
fn push<T>(a: [T], elem: T) external
fn set_value<T>(a: [T], index: int, value: T) external
fn sort<T>(a: [T]) external

/// lib file and string utils
fn read_lines(filename: str) [str] external
fn split_str(s: str, by: str) [str] external
fn str_to_int(s: str) int external
fn int_to_str(i: int) str external
fn str_to_chars(s: str) [str] external
fn str_contains_str(s: str, subs: str) int external

/// main

fn log_int_array(a: [int])
   s = ''
   for a do s = s + ' ' + int_to_str(it)
   log(s)
end

fn get_pages_rank(pages: [int], rules: [int]) int 
   log_int_array(pages)

   incorrect = 0
   loop {
      swapped = 0
      for 0..(len(pages) - 2) {
         i = it
         a = pages[i]
         b = pages[i + 1]
         rule = b*100 + a
         if rules[rule] {
            set_value(pages, i, b)
            set_value(pages, i + 1, a)
            swapped = 1
         }
      }

      if swapped = 0 do break
      incorrect = 1
   }

   log_int_array(pages)
   log()

   if incorrect do ret pages[len(pages)/2]
   ret 0
end

fn run
   lines = read_lines('D:/src/postal/aoc2024/input.txt')

   rules = [int]
   set_size(rules, 10000)

   total = 0
   for lines {
      if it = '' do continue
      
      if str_contains_str(it, '|') {
         parts = split_str(it, '|')
         rule = str_to_int(parts[0])*100 + str_to_int(parts[1])
         set_value(rules, rule, 1)
      }

      if str_contains_str(it, ',') {
         pages = [int]
         pages_str = split_str(it, ',')
         for pages_str {
            push(pages, str_to_int(it))
         }
         total = total + get_pages_rank(pages, rules)
      }
   }

   log(total, 'done')
end
